version = '0.17.0'

apply plugin: 'java'
apply plugin: 'application'

dependencies {
    compile project(':selendroid-common')
    compile project(':selendroid-server-common')
    compile 'org.apache.httpcomponents:httpclient:4.3.4'
    compile 'org.json:json:20090211'
    compile 'commons-io:commons-io:2.2'
    compile 'org.apache.commons:commons-exec:1.1'
    compile 'org.apache.commons:commons-compress:1.5'
    compile 'com.beust:jcommander:1.30'
    compile 'com.android.tools.ddms:ddmlib:23.0.1'
    compile 'com.google.guava:guava:17.0'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile('org.seleniumhq.selenium:selenium-java:2.47.2') {
        exclude(module: 'selenium-htmlunit-driver')
    }
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

mainClassName = 'io.selendroid.standalone.SelendroidLauncher'

jar {
    archiveName = 'selendroid-standalone-'
    archiveName += version
    archiveName += '-with-dependencies.jar'

    // Version from manifest attributes will be retrieved while building selendroid-server apk
    manifest {
        attributes('Main-Class': "$mainClassName", 'version': '0.18.0-SNAPSHOT')
    }

    into('prebuild') {
        from {
            ['../selendroid-server/build/outputs/apk/selendroid-server-0.18.0-SNAPSHOT.apk',
             '../android-driver/build/outputs/apk/android-driver-app-0.18.0-SNAPSHOT.apk']
        }
    }

    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    from {
        ['./AndroidManifestTemplate.xml',
         '../README.md']
    }
}
